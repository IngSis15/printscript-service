import org.jlleitschuh.gradle.ktlint.tasks.KtLintCheckTask

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
    id 'org.jetbrains.kotlinx.kover' version '0.4.0'
}

group = 'edu.ingsis'
version = '0.0.1-SNAPSHOT'



java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/IngSis15/PrintScript")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.example:runner:1.1.35'
    implementation 'org.example:lib:1.1.35'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:2.6.0"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

ktlint {
    version.set("0.41.0")
    android.set(false)
    outputToConsole.set(true)
    ignoreFailures.set(false)
    enableExperimentalRules.set(true)
    additionalEditorconfigFile.set(file(".editorconfig"))
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

tasks.withType(KtLintCheckTask).configureEach {
    dependsOn("ktlintFormat")
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyPreCommitHook', Copy) {
    description = "Copy pre-commit git hook from the scripts to the .git/hooks folder."
    group = "git hooks"
    from "hooks/pre-commit"
    into ".git/hooks"
    doLast {
        def hookFile = file('.git/hooks/pre-commit')
        if (hookFile.exists()) {
            hookFile.setExecutable(true)
        }
    }
}

tasks.build.dependsOn copyPreCommitHook
